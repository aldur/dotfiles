" Modeline and Notes {{{
" vim: set foldlevel=0 foldmethod=marker spell formatoptions=jcrql:
" }}}

" Initialization {{{
    " Define a global autogroup used through the entire vimrc
    augroup vimrc
        autocmd!
    augroup end

    " {{{ Providers
        " This needs to be done before loading plugins, as they might
        " require/load providers.

        " Disable non-required providers
        if has('nvim')
            let g:loaded_python_provider = 0  " Disable Python 2 support.
            let g:loaded_ruby_provider = 0  "   Disable Ruby support.
            let g:loaded_node_provider = 0  "   Disable nodeJS support.
            let g:loaded_perl_provider = 0  "   Disable Perl support

            " Replace `filetype.vim` for `filetype.lua` for faster startup.
            " let g:do_filetype_lua = 1
            " let g:did_load_filetypes = 0
        endif

        " Path to homebrew Python3 for nvim
        if filereadable('/usr/local/bin/python3')
            let g:python3_host_prog = '/usr/local/bin/python3'
        elseif filereadable('/opt/homebrew/bin/python3')
            let g:python3_host_prog = '/opt/homebrew/bin/python3'
        end

        " Disable some of VIM standard plugins
        let g:loaded_2html_plugin = 1  " Disable tohtml.vim
        let g:loaded_tutor_mode_plugin = 1  " Disable vimtutor
        let g:loaded_vimballPlugin = 1  " Disable vimball
        let g:loaded_tarPlugin = 1  " Disable tar
        let g:loaded_getscriptPlugin = 1  " Disable getscript
        let g:loaded_zipPlugin = 1  " Disable zip
    " }}}
" }}}

" Vim-plug {{{
    " Auto-install Vim-plug (if needed)
    if empty(glob('~/.vim/autoload/plug.vim')) && executable('curl')
        silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
                    \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
        autocmd vimrc VimEnter * PlugInstall
    endif
    call plug#begin('~/.vim/plugged')

    " The list of installed plugins
    Plug 'Konfekt/FastFold'                        " Better fold refresh
    Plug 'keith/investigate.vim'                   " Smart documentation finder
    Plug 'romainl/vim-qf'                          " Fix the quickfix
    Plug 'tmsvg/pear-tree'                         " Auto-pair
    Plug 'tommcdo/vim-lion'                        " Add gl/gL to align text

    Plug 'tpope/vim-commentary'                    " Toggle comments
    Plug 'tpope/vim-repeat'                        " Enable '.' for plugin actions
    Plug 'tpope/vim-surround'                      " All about surrounding
    Plug 'tpope/vim-scriptease'                    " Easier plugin development
    Plug 'tpope/vim-unimpaired'                    " Complementary mappings
    Plug 'tpope/vim-dispatch'                      " Async job execution

    " This does multiple things:
    " - :%Subvert/facilit{y,ies}/building{,s}/g
    " - fooBar -> `crs` -> foo_bar
    Plug 'tpope/vim-abolish'

    if executable('git')
        Plug 'tpope/vim-fugitive'                      " A GIT wrapper
        Plug 'tpope/vim-rhubarb'                       " Allow `Gbrowse`
    endif

    Plug 'mbbill/undotree', { 'on': 'UndotreeToggle' }

    " Fuzzy files, tags, life.
    if has('mac') && executable('fzf')  " Install `fzf` with homebrew
        if filereadable('/usr/local/opt/fzf')
            Plug '/usr/local/opt/fzf' | Plug 'junegunn/fzf.vim'
        else
            " M1 macs
            Plug '/opt/homebrew/opt/fzf' | Plug 'junegunn/fzf.vim'
        endif
    else  " Install it as a local plugin
        Plug 'junegunn/fzf', { 'do': { -> fzf#install() } }
        Plug 'junegunn/fzf.vim'
    end

    " Wiki
    Plug 'lervag/wiki.vim'
    Plug 'lervag/lists.vim'
    if executable('rg')
        Plug 'aldur/notational-fzf-vim'
    end

    " Appearance and Statusline
    Plug 'itchyny/lightline.vim'                  " Configure the statusbar

    Plug 'sainnhe/sonokai'                        " Based on Monokai pro

    Plug 'folke/zen-mode.nvim'

    " Language specific plugins {{{
        " Proper Python indent
        Plug 'Vimjas/vim-python-pep8-indent', { 'for': 'python' }

        " Latex
        Plug 'lervag/vimtex', { 'for': 'tex' }

        " Go
        Plug 'fatih/vim-go', { 'for': 'go' }

        " Markdown
        Plug 'aldur/vim-markdown', { 'for': 'markdown' }

        " Swift
        Plug 'keith/swift.vim', { 'for': 'swift' }

        " Fish
        Plug 'georgewitteman/vim-fish', { 'for': 'fish' }

        " Kotlin
        Plug 'udalov/kotlin-vim', { 'for': 'kotlin' }

        " Nginx
        Plug 'chr4/nginx.vim', { 'for': 'nginx' }

        " Rust
        Plug 'rust-lang/rust.vim', { 'for': 'rust' }

        " Algorand's TEAL
        Plug 'aldur/vim-algorand-teal'

        " Solidity
        Plug 'TovarishFin/vim-solidity', { 'for': 'solidity' }
    " }}}

    let b:has_nvim_python3 = has('nvim') && has('python3')

    Plug 'neovim/nvim-lspconfig', b:has_nvim_python3 ? {  } : { 'on': [] }
    Plug 'ray-x/lsp_signature.nvim', b:has_nvim_python3 ? {  } : { 'on': [] }
    Plug 'folke/trouble.nvim', b:has_nvim_python3 ? {  } : { 'on': [] }
    Plug 'kosayoda/nvim-lightbulb', b:has_nvim_python3 ? {  } : { 'on': [] }

    " Improve Neovim responsiveness around CursorHold/CursorHoldI events
    Plug 'antoinemadec/FixCursorHold.nvim', b:has_nvim_python3 ? {  } : { 'on': [] }

    " Completion {{{ "
        Plug 'hrsh7th/cmp-nvim-lsp', b:has_nvim_python3 ? {  } : { 'on': [] }
        Plug 'hrsh7th/cmp-buffer', b:has_nvim_python3 ? {  } : { 'on': [] }
        Plug 'hrsh7th/nvim-cmp', b:has_nvim_python3 ? {  } : { 'on': [] }
        Plug 'hrsh7th/cmp-path', b:has_nvim_python3 ? {  } : { 'on': [] }
        Plug 'hrsh7th/cmp-nvim-lua', b:has_nvim_python3 ? {  } : { 'on': [] }
        Plug 'hrsh7th/cmp-cmdline', b:has_nvim_python3 ? {  } : { 'on': [] }
        Plug 'quangnguyen30192/cmp-nvim-tags', b:has_nvim_python3 ? {  } : { 'on': [] }
    " }}} Completion "

    if executable('ctags')
        Plug 'ludovicchabant/vim-gutentags'            " Automatic tag management
    endif

    " Snippets  {{{
        " Conditionally load `ultisnips` if on `nvim` with `python3`.
        " Snippets engine
        Plug 'SirVer/ultisnips', b:has_nvim_python3 ? {} : { 'on': [] }
        " Vim snippets
        Plug 'honza/vim-snippets', b:has_nvim_python3 ? {} : { 'on': [] }

        " Completion plugin.
        Plug 'quangnguyen30192/cmp-nvim-ultisnips', b:has_nvim_python3 ? {  } : { 'on': [] }
    " }}}

    " Update treesitter parsers when updating the plugin itself
    Plug 'nvim-treesitter/nvim-treesitter',
                \ b:has_nvim_python3 ? { 'do': ':TSUpdate' }
                \ : { 'on': []}
    Plug 'nvim-treesitter/nvim-treesitter-textobjects'
    Plug 'RRethy/nvim-treesitter-textsubjects'
    Plug 'lewis6991/spellsitter.nvim'
    Plug 'romgrk/nvim-treesitter-context'
    Plug 'nvim-treesitter/playground'

    " Highlight last yanked object.
    " nvim provides this out of the box.
    Plug 'machakann/vim-highlightedyank',
                \ b:has_nvim_python3 ? { 'on': []} : {}

    " ...Vim-plug is done
    call plug#end()

    " Load the `matchit` plugin from VIM's runtime.
    packadd! matchit
" }}}

" General {{{
    " Sets how many lines of history VIM has to remember
    set history=10000 " Maximum value for history

    " Modeline in the first three lines
    set modeline
    set modelines=3

    " Mouse
    set mouse=a
    set mousehide               " Hide the mouse cursor while typing

    set updatetime=500         " ms to trigger the CursorHold/CursorHoldI events
    let g:cursorhold_updatetime = &updatetime

    " With a map leader it's possible to do extra key combinations.
    " We set it here so it is effective for all `plugin/` files.
    let g:mapleader = "\<Space>"
    let g:maplocalleader = "\<Space>"
" }}}

" Text, lines, tab, indent and folding {{{
    set backspace=indent,eol,start                     " Backspace for dummies
    set whichwrap=b,s,h,l,<,>,[,]                      " Backspace and cursor keys wrap too

    " Line wrapping
    set wrap                                           " Visually wrap lines too long
    set textwidth=0 wrapmargin=0                       " Turn off physical line wrapping
    set breakindent                                    " Preserve indent of wrapped lines

    " Do not insert comment leader on 'o'
    " Needs an autocmd because different filetypes override this option
    autocmd vimrc FileType * setlocal formatoptions-=o
" }}}

" Plugin settings {{{
    " Investigate {{{
        " Use Dash on macOS (if available)
        let g:investigate_use_dash=1

        nnoremap <silent> K :call investigate#Investigate('n')<CR>
        vnoremap <silent> K :call investigate#Investigate('v')<CR>
    " }}}

    " lion {{{
        let g:lion_map_right = 'gR'  " Prevent conflict with vim.wiki
    " }}}

    " lists.vim {{{
        let g:lists_filetypes = ['md']
    " }}}

    " vim.wiki {{{
        let g:wiki_root = '~/Documents/Notes'
        let g:wiki_filetypes = ['md']
        let g:wiki_link_target_type = 'md'

        " Disable unused mappings
        let g:wiki_mappings_global = {
                    \ '<plug>(wiki-index)' : '<plug>',
                    \ '<plug>(wiki-open)' : '<plug>',
                    \ '<plug>(wiki-reload)' : '<plug>',
                    \ '<plug>(wiki-page-toc)' : '<plug>',
                    \ '<plug>(wiki-page-toc-local)' : '<plug>',
                    \ '<plug>(wiki-page-rename)' : '<plug>',
                    \ '<plug>(wiki-tag-list)' : '<plug>',
                    \ '<plug>(wiki-tag-reload)' : '<plug>',
                    \ '<plug>(wiki-tag-search)' : '<plug>',
                    \ '<plug>(wiki-link-next)' : '<plug>',
                    \ '<plug>(wiki-link-prev)' : '<plug>',
                    \ '<plug>(wiki-link-toggle)' : '<plug>',
                    \ '<plug>(wiki-link-open)' : '<plug>',
                    \ '<plug>(wiki-export)' : '<plug>',
                    \ '<plug>(wiki-rename)' : '<plug>',
                    \}

        let g:wiki_mappings_local = g:wiki_mappings_global

        let g:wiki_list_todos = ['TODO', 'DONE']

        function! TemplateWrapper(context) abort
            call aldur#wiki#template(a:context)
        endfunction

        function! TemplateMatcher(context) abort
            " Only match if within the wiki root
            return a:context.path =~ '^' . g:wiki_root
        endfunction

        " Catch-all template.
        let g:wiki_templates = [
            \ {
            \   'match_func': function('TemplateMatcher'),
            \   'source_func': function('TemplateWrapper')
            \ },
            \ ]

        " Fix temporary error in creating links.
        " let g:wiki_map_link_create = 'aldur#wiki#map_link_create'

        " Configure `notational-fzf-vim`
        let g:nv_main_directory = g:wiki_root
        let g:nv_search_paths = [g:wiki_root, ]  " This is for backward-compatibility only
        let g:nv_create_note_window = 'edit'
        let g:nv_use_short_pathnames = 0
    " }}}
" }}}

" Ending settings {{{
    if filereadable(expand('~/.vimrc.local'))
        source ~/.vimrc.local
    endif

    set secure
" }}}
