" Modeline and Notes {{{
" vim: set foldlevel=0 foldmethod=marker spell formatoptions=jcrql:
" }}}

" Initialization {{{
    " Define a global autogroup used through the entire vimrc
    augroup vimrc
        autocmd!
    augroup end
" }}}

" Vim-plug {{{
    " Auto-install Vim-plug (if needed)
    if empty(glob('~/.vim/autoload/plug.vim')) && executable('curl')
        silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
                    \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
        autocmd vimrc VimEnter * PlugInstall
    endif
    call plug#begin('~/.vim/plugged')

    " The list of installed plugins
    Plug 'Konfekt/FastFold'             " Better fold refresh
    Plug 'cohama/lexima.vim'            " Wonderful auto-pair
    Plug 'honza/vim-snippets'           " Vim snippets
    Plug 'keith/investigate.vim'        " Smart documentation finder
    Plug 'ludovicchabant/vim-gutentags' " Automatic tag management
    Plug 'neomake/neomake'              " Asynchronous Syntax Checkers
    Plug 'sheerun/vim-polyglot'         " Additional syntaxes / indent
    Plug 'tommcdo/vim-lion'             " Add gl/gL to align text
    Plug 'tpope/vim-commentary'         " Toggle comments
    Plug 'tpope/vim-repeat'             " Enable supported plugin actions to be repeated with '.'
    Plug 'tpope/vim-surround'           " All about surrounding
    Plug 'wellle/targets.vim'           " Additional text objects

    Plug '/usr/local/opt/fzf' | Plug 'junegunn/fzf.vim'

    " Statusline
    Plug 'itchyny/lightline.vim'        " Configure the statusbar / tabline
    Plug 'taohex/lightline-buffer'      " Buffers in tabline

    Plug 'sbdchd/neoformat',
                \ { 'on': 'Neoformat' } " Code auto-formatting

    " Lazy loaded plugins / platform specific plugins
    Plug 'SirVer/ultisnips',
                \ { 'on': [] } " Snippets engine

    Plug 'Shougo/deoplete.nvim',
                \ has('nvim') ? {'do': ':UpdateRemotePlugins'}
                \ : { 'on': [] } " Completion engine - only works with NeoVim

    " Appearance
    Plug 'junegunn/rainbow_parentheses.vim',
                \ { 'on' : 'RainbowParentheses'}  " Rainbow parentheses
    Plug 'fatih/molokai'                          " Molokai color scheme

    " Language specific plugins {{{
        " Python
        Plug 'jmcantrell/vim-virtualenv',
                    \ { 'for': 'python' }            " Virtualenv within VIM

        " Markdown
        Plug 'suan/vim-instant-markdown',
                    \ { 'for' : 'markdown' }         " Instant markdown preview in browser (requirements on GitHub)

        " Latex
        Plug 'lervag/vimtex', { 'for': 'tex' }

        " Go
        Plug 'fatih/vim-go', { 'for': 'go' }
    " }}}

    " Deoplete specific plugins {{{
        Plug 'zchee/deoplete-jedi',
                    \ has('nvim') ? {  } : { 'on': [] }              " Python completion
        Plug 'zchee/deoplete-go',
                    \ has('nvim') ? { 'do': 'make' } : { 'on': [] }  " Go completion
        Plug 'zchee/deoplete-clang',
                    \ has('nvim') ? {  } : { 'on': [] }              " Clang completion
        Plug 'ujihisa/neco-look',
                    \ has('nvim') ? {  } : { 'on': [] }              " Spell completion
        Plug 'Shougo/neco-vim',
                    \ has('nvim') ? {  } : { 'on': [] }              " VIM completion
    " }}}

    " Enable Vim-stay after all the other plugins
    Plug 'kopischke/vim-stay'                 " Save and restore cursor position, folds and so on

    " ...Vim-plug is done
    call plug#end()
" }}}

" General {{{
    " Sets how many lines of history VIM has to remember
    set history=10000 " Maximum value for history

    " Modeline in the first three lines
    set modeline
    set modelines=3

    " Mouse
    set mouse=a
    set mousehide               " Hide the mouse cursor while typing

    " End of line (unix EOL is preferred over the dos one and before the mac one).
    set fileformats=unix,dos,mac

    " With a map leader it's possible to do extra key combinations
    let g:mapleader = "\<Space>"
    let g:maplocalleader = "\<Space>"
" }}}

" Backup, swap, info and ignore files {{{
    set noswapfile  " Stop annoying swap files

    " Save your backups to a less annoying place than the current directory.
    " If you have .vim-backup in the current directory, it'll use that.
    " Otherwise it saves it to ~/.vim/backup or . if all else fails.
    if isdirectory($HOME . '/.vim/backup') == 0
        :silent !mkdir -p ~/.vim/backup >/dev/null 2>&1
    endif
    set backupdir-=.
    set backupdir+=.
    set backupdir-=~/
    set backupdir^=~/.vim/backup/
    set backupdir^=./.vim-backup/
    set backup

    " Save your swap files to a less annoying place than the current directory.
    " If you have .vim-swap in the current directory, it'll use that.
    " Otherwise it saves it to ~/.vim/swap, ~/tmp or .
    if isdirectory($HOME . '/.vim/swap') == 0
        :silent !mkdir -p ~/.vim/swap >/dev/null 2>&1
    endif
    set directory=./.vim-swap//
    set directory+=~/.vim/swap//
    set directory+=~/tmp//
    set directory+=.

    " Tell (n)vim to remember certain things when we exit
    "  '100  :  marks will be remembered for up to 100 previously edited files
    "  "100 :  will save up to 100 lines for each register
    "  <100 :  will save up to 100 lines for each register (new alternative to ")
    "  :1000  :  up to 1000 lines of command-line history will be remembered
    "  %    :  saves and restores the buffer list
    "  n... :  where to save the viminfo files
    if has('nvim')
        set shada='100,<100,:10000,%,n~/.nvim/nviminfo
        set viewdir=~/.vim/view
    else
        set viminfo='100,\"100,:10000,%,n~/.vim/viminfo
    endif

    " undofile - This allows you to use undos after exiting and restarting
    " This, like swap and backups, uses .vim-undo first, then ~/.vim/undo
    " :help undo-persistence
    " This is only present in 7.3+
    if isdirectory($HOME . '/.vim/undo') == 0
        :silent !mkdir -p ~/.vim/undo > /dev/null 2>&1
    endif
    set undodir=./.vim-undo//
    set undodir+=~/.vim/undo//
    set undofile

    " Ignore unreadable files, images, etc.
    set wildignore=*.o,*~,*.pyc
    set wildignore+=*.png,*.gif,*.jpg,*.ico
    set wildignore+=.git,.svn,.hg
    set wildignore+=*/tmp/*,*.so,*.swp,*.zip
    set wildignore+=.DS_Store

    " Save cursor position, folds, and so on.
    set viewoptions=cursor,folds,slash,unix
" }}}

" Appearance {{{
    "Always show current position
    set cursorline                   " highlights the current line

    " Height of the command bar
    set cmdheight=1

    set background=dark
    colorscheme molokai

    set scrolloff=8       " don't scroll any closer to top/bottom
    set sidescrolloff=5   " don't scroll any closer to left/right

    " No annoying sound on errors
    set noerrorbells visualbell t_vb=
    autocmd vimrc GUIEnter * set visualbell t_vb=

    " Line and relative numbers
    set number
    set relativenumber

    set showmatch                   " Show matching brackets/parenthesis
    set matchtime=2                 " How many tenths of seconds to blink matching bracket/parenthesis

    set linespace=0                 " No extra spaces between rows in GUI

    " Allow the cursor to be one more the last char.
    set virtualedit=onemore

    " Disable split separator characters
    set fillchars=""

    " Show snippet_complete marker (if any)
    if has('conceal')
        set conceallevel=2 concealcursor=i
    endif

    " Only show cursorline in current window
    autocmd vimrc WinEnter * set cursorline
    autocmd vimrc WinLeave * set nocursorline
" }}}

" Auto completion {{{
    " How to show completion informations in insert mode
    set completeopt=menuone,longest

    " Clever tab to cycle the completion popup menu
    " If you even need to insert a literal tab, press <CTRL-V><Tab>
    inoremap <silent><expr> <TAB>
                \ pumvisible() ? "\<C-n>" :
                \ <SID>check_back_space() ? "\<TAB>" :
                \ deoplete#manual_complete()
    function! s:check_back_space() abort "{{{
        let l:col = col('.') - 1
        return !l:col || getline('.')[l:col - 1]  =~? '\s'
    endfunction"}}}

    if has('patch-7.4.314')
        set shortmess+=c " Quiet completions
    endif
" }}}

" Search and commands {{{
    set showcmd               " Display incomplete commands

    " Magic pattern matching
    set magic

    set incsearch             " Find as you type search
    set hlsearch              " Highlight search terms
    set ignorecase            " Case insensitive search
    set smartcase             " Case sensitive when upper case is present
    set gdefault

    if has('nvim')
        " Preview command results.
        set inccommand=nosplit
    endif

    set wildmenu              " Show list instead of just completing
    set wildmode=longest,full " Command <Tab> completion, list matches, then longest common part, then all.

    " Use sane magic regexes
    nnoremap / /\v
    vnoremap / /\v
" }}}

" Text, lines, tab, indent and folding {{{
    " Set utf-8 as standard encoding
    if !has('nvim') " NeoVim defaults to this
        set encoding=utf-8                             " The encoding displayed.
    end
    scriptencoding utf-8                               " Specify the encoding of this file
    set fileencoding=utf-8                             " The encoding written to file.

    set backspace=indent,eol,start                     " Backspace for dummies
    set whichwrap=b,s,h,l,<,>,[,]                      " Backspace and cursor keys wrap too

    " White spaces
    set list                                           " Display white spaces
    set listchars=tab:→\ ,trail:•,                     " Custom characters highlights
    set listchars+=extends:⟩,precedes:⟨,
    set listchars+=\nbsp:␣,conceal:*,
    let &showbreak = '↪'                               " Show whether lines have been wrapped

    " Tabs
    set expandtab                                      " In insert mode, insert the appropriate number of spaces to insert a Tab
    set smarttab                                       " Insert tabs in front of lines according to shiftwidth
    set shiftwidth=4                                   " Operation >> indents 4 columns; << un-indents 4 columns
    set tabstop=4                                      " A hard TAB displays as 4 columns
    set expandtab                                      " Insert spaces when hitting TABs
    set softtabstop=4                                  " Insert/delete 4 spaces when hitting a TAB/BACKSPACE
    set shiftround                                     " Round indent to multiple of 'shiftwidth'
    set autoindent                                     " Align the new line indent with the previous line
    set cindent                                        " Automatic C programming indenting

    " Line wrapping
    set wrap                                           " Visually wrap lines too long
    set textwidth=0 wrapmargin=0                       " Turn off physical line wrapping

    " Do not insert comment leader on 'o'
    " Needs an autocmd because different filetypes override this option
    autocmd vimrc FileType * setlocal formatoptions-=o

    " Filetype specific settings {{{
        " Git commit
        autocmd vimrc Filetype gitcommit setlocal spell textwidth=72

        " Latex
        autocmd vimrc Filetype tex setlocal spell conceallevel=0

        " Markdown
        autocmd vimrc Filetype markdown,md setlocal spell
    " }}}
" }}}

" Tabs, windows and buffers {{{
    set hidden  " Allows to change buffer without saving

    " Open new split panes to right and bottom, which feels more natural
    set splitbelow splitright

    " Set to auto read when a file is changed from the outside
    set autoread

    " Switch CWD to the directory of the open buffer
    set autochdir

    " Auto save on :next, :edit, :quit, etc.
    set autowriteall

    " ... and on focus lost
    autocmd vimrc FocusLost * :silent! wall

    " Smart way to move between windows
    nnoremap <C-h> <C-w>h
    nnoremap <C-j> <C-w>j
    nnoremap <C-k> <C-w>k
    nnoremap <C-l> <C-w>l

    " Close the current buffer
    nnoremap <leader>bd :bd<cr>

    " Close all the buffers
    nnoremap <leader>ba :bufdo bdelete<cr>

    " Move to next buffer
    nnoremap <tab> :bnext<cr>
    nnoremap <s-tab> :bprevious<cr>

    nnoremap <leader>bn :bnext<cr>
    nnoremap <leader>bp :bprevious<cr>

    " Useful mappings for managing tabs
    nnoremap <leader>tn :tabnew<cr>
    nnoremap <leader>to :tabonly<cr>
    nnoremap <leader>tc :tabclose<cr>
    nnoremap <leader>tm :tabmove

    " Specify the behavior when switching between buffers
    try
        set switchbuf=useopen,usetab,newtab
        set showtabline=2
    catch
    endtry

    " Code folding mappings
    nmap <leader>f0 :set foldlevel=0<CR>
    nmap <leader>f1 :set foldlevel=1<CR>
    nmap <leader>f2 :set foldlevel=2<CR>
    nmap <leader>f3 :set foldlevel=3<CR>

    " Exclude quickfix windows from buflist
    autocmd vimrc FileType qf set nobuflisted

    " Resize splits when the window is resized
    autocmd vimrc VimResized * :wincmd =
" }}}

" Misc mappings {{{
    " Remap VIM 0 to first non-blank character
    map 0 ^

    " Treat long lines as break lines (useful when moving around in them)
    noremap j gj
    noremap k gk
    noremap gj j
    noremap gk k

    " Disable highlight when <leader><cr> is pressed
    nnoremap <silent> <leader><cr> :noh<cr>

    " Toggle paste mode on and off
    nnoremap <leader>pp :setlocal paste!<cr>

    " Make Y yank everything from the cursor to the end of the line.
    noremap Y y$

    " Quickly call 'make'
    nnoremap <leader>m :make<cr>

    " Loclist mappings
    nnoremap <leader>lo :lopen<cr>
    nnoremap <leader>ln :lnext<cr>
    nnoremap <leader>lp :lprev<cr>

    " Use 'c*' to change the word under the cursor, repeat with '.'
    nnoremap c* *<C-o>cgn

    if &term =~? '^screen'
        " tmux will send xterm-style keys when xterm-keys is on
        execute "set <xUp>=\e[1;*A"
        execute "set <xDown>=\e[1;*B"
        execute "set <xRight>=\e[1;*C"
        execute "set <xLeft>=\e[1;*D"
    endif
" }}}

" Spell checking {{{
    " Shortcuts
    nnoremap <leader>ss :setlocal spell!<cr>
" }}}

" Misc {{{
    set iskeyword-=.                    " '.' is an end of word designator
    set iskeyword-=#                    " '#' is an end of word designator
    set iskeyword-=-                    " '-' is an end of word designator

    " Highlight TODO, FIXME, NOTE, etc.
    autocmd vimrc Syntax * call matchadd('Todo',  '\W\zs\(TODO\|FIXME\|CHANGED\|XXX\|BUG\|HACK\)')
    autocmd vimrc Syntax * call matchadd('Debug', '\W\zs\(NOTE\|INFO\|IDEA\|DEBUG\)')

    if executable('ag')
        set grepprg=ag\ --vimgrep\ $*
        set grepformat=%f:%l:%c:%m
    elseif executable('ack')
        set grepprg=ack\ -k
    endif

    if has('nvim')
        set termguicolors
    endif

    " Templates support.
    if isdirectory($HOME . '/.vim/backup') == 1
        autocmd vimrc BufNewFile *.py 0r ~/.vim/templates/skeleton.py
    endif
" }}}

" Helper functions {{{
    " Strip whitespace {{{
    function! StripTrailingWhitespace()
        " Preparation: save cursor position
        " (last search is automatically saved because inside a function)
        let l:search = @/
        let l:save = winsaveview()

        " Do the business.
        " vint: -ProhibitCommandRelyOnUser -ProhibitCommandWithUnintendedSideEffect
        %s/\s\+$//ei

        " Clean up: restore cursor position.
        " vint: +ProhibitCommandRelyOnUser +ProhibitCommandWithUnintendedSideEffect
        let @/=l:search
        call winrestview(l:save)
    endfunction
    " }}}

    " Before saving, delete trailing white-spaces and ^M
    autocmd vimrc FileType c,cpp,java,php,javascript,python,twig,xml,yaml,vim,tex,lua
                \ autocmd vimrc BufWritePre <buffer> call StripTrailingWhitespace()

    " Return the number of lines in a window, between minheight and maxheight
    function! AdjustWindowHeight(minheight, maxheight)
        execute max([a:minheight, min([line('$') + 1, a:maxheight])])
                    \ . 'wincmd _'
    endfunction

    " Automatically resize the quickfix/loclist window
    autocmd vimrc BufReadPost quickfix set modifiable |
                \ call AdjustWindowHeight(2, 10) |
                \ set nomodifiable
" }}}

" Plugin settings {{{

    " netrw {{{
        " Not an external plugin, but still... :)
        let g:netrw_banner = 0  " Disable top banner
        let g:netrw_silent = 1  " Do not output `scp` commands etc, while saving.
        let g:netrw_liststyle = 3  " Tree style
        let g:netrw_browse_split = 4  " Open new files in previous window
        let g:netrw_altv = 1  " Split to right
        let g:netrw_winsize = 25  " Window height/width on split
        let g:netrw_list_hide = netrw_gitignore#Hide()  " Hide git-ignored files

        nnoremap - :Vexplore<cr>
    " }}}

    " Lightline {{{
        set noshowmode   " Do not show mode indicator below status bar
        set laststatus=2 " Always show the statusbar

        let g:lightline = {}

        " Setup the single components
        let g:lightline.component = {
                    \ 'mode': '%{lightline#mode()}',
                    \ 'absolutepath': '%F',
                    \ 'relativepath': '%f',
                    \ 'filename': '%t',
                    \ 'modified': '%M',
                    \ 'bufnum': '%n',
                    \ 'paste': '%{&paste?"P":""}',
                    \ 'readonly': '%R',
                    \ 'charvalue': '%b',
                    \ 'charvaluehex': '%B',
                    \ 'fileencoding': '%{&fenc!=#""?&fenc:&enc}',
                    \ 'fileformat': '%{&ff}',
                    \ 'filetype': '%{&ft!=#""?&ft:"no ft"}',
                    \ 'percent': '%3p%%',
                    \ 'percentwin': '%P',
                    \ 'spell': '%{&spell?&spelllang:""}',
                    \ 'lineinfo': '%3l:%-2v',
                    \ 'line': '%l',
                    \ 'column': '%c',
                    \ 'close': '%999X X ' }

        let g:lightline.component_function = {
                    \ 'mode': 'LightlineMode',
                    \ 'bufferbefore': 'lightline#buffer#bufferbefore',
                    \ 'bufferafter': 'lightline#buffer#bufferafter',
                    \ 'bufferinfo': 'lightline#buffer#bufferinfo',
                    \ }

        let g:lightline.component_expand = {
                    \ 'tabs': 'lightline#tabs',
                    \ 'buffercurrent': 'lightline#buffer#buffercurrent2',
                    \ 'virtualenv': 'LightlineVirtualenv',
                    \ 'syntax_error': 'LightlineNeomakeError',
                    \ 'syntax_warning': 'LightlineNeomakeWarning',
                    \ 'syntax_info': 'LightlineNeomakeInfo',
                    \ }

        let g:lightline.component_type = {
                    \ 'syntax_error': 'error',
                    \ 'syntax_warning': 'warning',
                    \ 'syntax_info': 'info',
                    \ 'buffercurrent': 'tabsel',
                    \ }

        " Setup the active status bar
        let g:lightline.active = {
                    \ 'left': [ [ 'mode', 'paste', 'spell' ],
                    \           [ 'readonly', 'filename', 'modified' ] ],
                    \ 'right': [ [ 'syntax_error', 'syntax_warning', 'syntax_info' ],
                    \            [ 'lineinfo' ],
                    \            [ 'fileformat', 'fileencoding', 'filetype' ],
                    \            [ 'virtualenv' ] ] }

        " Setup the inactive status bar
        let g:lightline.inactive = {
                    \ 'left': [ [ 'filename' ] ],
                    \ 'right': [ [ 'lineinfo' ],
                    \            [ 'percent' ] ] }

        " Setup the tabline
        let g:lightline.tabline = {
                    \ 'left': [ [ 'bufferinfo' ], [ 'bufferbefore', 'buffercurrent', 'bufferafter' ], ],
                    \ 'right': [ [ 'close' ] ] }

        function! LightlineMode()
            return winwidth(0) > 60 ? lightline#mode() : ''
        endfunction

        " Virtualenv integration
        function! LightlineVirtualenv()
            return &filetype ==? 'python' ? virtualenv#statusline() : ''
        endfunction

        " Neomake integration {{{
            function! LightlineNeomakeError()
                let l:errors = get(neomake#statusline#LoclistCounts(), 'E', 0)
                return l:errors > 0 ? 'E: '.l:errors : ''

                let l:warning = get(neomake#statusline#LoclistCounts(), 'E', 0)
                let l:infos = get(neomake#statusline#LoclistCounts(), 'E', 0)
            endfunction

            function! LightlineNeomakeWarning()
                let l:warnings = get(neomake#statusline#LoclistCounts(), 'W', 0)
                return l:warnings > 0 ? 'W: '.l:warnings : ''
            endfunction

            function! LightlineNeomakeInfo()
                let l:infos = get(neomake#statusline#LoclistCounts(), 'I', 0)
                return l:infos > 0 ? 'I: '.l:infos : ''
            endfunction
        " }}}
    " }}}

    " Neomake {{{
        let g:neomake_airline = 0  " Disable airline integration

        " Run Neomake on save / WinEnter
        autocmd vimrc BufWritePost,BufWinEnter * silent Neomake

        function! NeomakeDidFinish()
            " Update lightline
            call lightline#update()
            " Close loclist if no errors
            if get(neomake#statusline#LoclistCounts(), 'E', 0) == 0 &&
                        \ get(neomake#statusline#LoclistCounts(), 'W', 0) == 0 &&
                        \ get(neomake#statusline#LoclistCounts(), 'I', 0) == 0
                lclose
            endif
        endfunction

        autocmd User NeomakeFinished call NeomakeDidFinish()

        " Filetype Specific Options {{{ "
            let g:neomake_tex_enabled_makers = ['chktex', 'lacheck', 'proselint']
        " }}} Filetype Specific Options "
    " }}}

    " Rainbow parentheses {{{
        " List of colors that you do not want. ANSI code or #RRGGBB
        let g:rainbow#blacklist = [225, 245, 241, 244, 193]

        let g:rainbow#pairs = [['(', ')'], ['[', ']'], ['{', '}']]
        let g:rainbow#max_level = 16
    " }}}

    " Deoplete {{{
        let g:deoplete#enable_at_startup = 1 " Enable Deoplete
        " let g:deoplete#auto_complete_delay = 20 " ms delay to trigger autocomplete

        let g:deoplete#sources = {}
        let g:deoplete#sources._ = ['buffer', 'member', 'tag', 'file', 'ultisnips']  " Disabled 'omni' 'cause it's not async
        let g:deoplete#sources.vim = g:deoplete#sources._ + ['vim']
        let g:deoplete#sources.tex = g:deoplete#sources._ + ['look', 'omni']
        let g:deoplete#sources.gitcommit = g:deoplete#sources._ + ['look']
        let g:deoplete#sources.python = g:deoplete#sources._ + ['jedi']
        let g:deoplete#sources.go = g:deoplete#sources._ + ['go']
        let g:deoplete#sources.c = g:deoplete#sources._ + ['clang']
        let g:deoplete#sources.cpp = g:deoplete#sources._ + ['clang']
        let g:deoplete#sources.objc = g:deoplete#sources._ + ['clang']

        let g:deoplete#max_list = 20 " Show 20 entries at most
        let g:deoplete#max_menu_width = 20 " Matches the list length

        let g:deoplete#skip_chars = ['(', ')']

        if has('nvim')
            call deoplete#custom#set('ultisnips', 'rank', 1000) " Keep snippets on top
        endif

        " Go completion settings
        let g:deoplete#sources#go#gocode_binary = $GOPATH.'/bin/gocode'
        let g:deoplete#sources#go#sort_class = ['package', 'func', 'type', 'var', 'const']
        let g:deoplete#sources#go#use_cache = 0

        " vim-tex integration
        if !exists('g:deoplete#omni#input_patterns')
            let g:deoplete#omni#input_patterns = {}
        endif
        let g:deoplete#omni#input_patterns.tex = '\\(?:'
                    \ .  '\w*cite\w*(?:\s*\[[^]]*\]){0,2}\s*{[^}]*'
                    \ . '|\w*ref(?:\s*\{[^}]*|range\s*\{[^,}]*(?:}{)?)'
                    \ . '|hyperref\s*\[[^]]*'
                    \ . '|includegraphics\*?(?:\s*\[[^]]*\]){0,2}\s*\{[^}]*'
                    \ . '|(?:include(?:only)?|input)\s*\{[^}]*'
                    \ . '|\w*(gls|Gls|GLS)(pl)?\w*(\s*\[[^]]*\]){0,2}\s*\{[^}]*'
                    \ . '|includepdf(\s*\[[^]]*\])?\s*\{[^}]*'
                    \ . '|includestandalone(\s*\[[^]]*\])?\s*\{[^}]*'
                    \ .')'
    " }}}

    " UltiSnips {{{ Remap UltiSnips for compatibility with completion handlers
        let g:UltiSnipsExpandTrigger                        = '<C-j>'
        let g:UltiSnipsJumpForwardTrigger                   = '<C-j>'
        let g:UltiSnipsJumpBackwardTrigger                  = '<C-k>'

        " This function only runs when UltiSnips is not loaded (credits github/saaguero)
        function! LoadUltiSnips()
            let l:curpos = getcurpos()
            execute plug#load('ultisnips')
            call cursor(l:curpos[1], l:curpos[2])
            call UltiSnips#ExpandSnippet()
            " Add command to manually list snippets
            command UltiSnipsListSnippets call UltiSnips#ListSnippets()
            return ''
        endfunction
        inoremap <silent> <C-j> <C-r>=LoadUltiSnips()<cr>

        " Give higher priority to UltiSnips specific snippets over SnipMate ones
        let g:UltiSnipsSnippetDirectories=[$HOME . '/.vim/plugged/vim-snippets/UltiSnips']
        let g:UltiSnipsEnableSnipMate=1

        " Snippets variables
        let g:author='Adriano Di Luzio'
        let g:snips_author=g:author
        let g:email='adrianodl@hotmail.it'
        let g:snips_email=g:email
        let g:github='https://github.com/aldur'
        let g:snips_github=g:github
    " }}}

    " Markdown {{{
        " Highlight YAML front matter.
        let g:vim_markdown_frontmatter = 1

        let g:vim_markdown_no_default_key_mappings = 1
        let g:vim_markdown_conceal = 0

        " let g:vim_markdown_fenced_languages = ['c++=cpp', 'viml=vim', 'bash=sh', 'ini=dosini', 'c', 'html', 'javascript', 'python']
    " }}}

    " Vim Instant Markdown {{{
        let g:instant_markdown_slow = 1
    " }}}

    " Vimtex settings {{{
        let g:vimtex_enabled = 1

        let g:tex_flavor = 'latex'  " Open '.tex' files as 'tex' and not 'plaintex'.
        let g:vimtex_quickfix_mode = 2  " Auto-open quickfix with no focus.

        if has('nvim')
            let g:vimtex_compiler_progname = '/usr/local/bin/nvr'
        end

        if has('mac')
            let g:vimtex_view_method = 'skim'
        end

        let g:vimtex_imaps_enabled = 0   " Disable insert mode mappings
        let g:vimtex_fold_enabled = 1    " Enable vimtex folds
        let g:vimtex_format_enabled = 1  " Enable better formatting with gq
    " }}}

    " Molokai {{{
        let g:molokai_original = 0 " Prefer gray background color
        let g:rehash256 = 1        " Enable experimental 256 colors support
    " }}}

    " Vim-Go {{{
        " Enable better syntax highlighting
        let g:go_highlight_functions = 1
        let g:go_highlight_methods = 1
        let g:go_highlight_fields = 1
        let g:go_highlight_structs = 1
        let g:go_highlight_interfaces = 1
        let g:go_highlight_operators = 1
        let g:go_highlight_build_constraints = 1

        " On recent VIM version better go fmt should work
        let g:go_fmt_experimental = 1

        " Show info for word under cursor
        let g:go_auto_type_info = 1

        " Mappings
        autocmd vimrc FileType go nmap <leader>r <Plug>(go-run)
        autocmd vimrc FileType go nmap <leader>b <Plug>(go-build)
        autocmd vimrc FileType go nmap <leader>t <Plug>(go-test)
        autocmd vimrc FileType go nmap <leader>c <Plug>(go-coverage)

        " autocmd vimrc FileType go nmap <Leader>ds <Plug>(go-def-split)
        " autocmd vimrc FileType go nmap <Leader>dv <Plug>(go-def-vertical)
        " autocmd vimrc FileType go nmap <Leader>dt <Plug>(go-def-tab)

        " autocmd vimrc FileType go nmap <Leader>gd <Plug>(go-doc)
        " autocmd vimrc FileType go nmap <Leader>gv <Plug>(go-doc-vertical)

        " autocmd vimrc FileType go nmap <Leader>e <Plug>(go-rename)
        autocmd vimrc FileType go nmap <Leader>i <Plug>(go-imports)
        " autocmd vimrc FileType go nmap <Leader>s <Plug>(go-implements)

        " Syntastic integration
        let g:go_list_type = 'quickfix'
    " }}}

    " vim-gutentags {{{
        if isdirectory($HOME . '/.vim/tags/') == 0
            :silent !mkdir -p ~/.vim/tags >/dev/null 2>&1
        endif

        let g:gutentags_cache_dir = $HOME . '/.vim/tags/'

        " Only select .git folders as project roots.
        let g:gutentags_add_default_project_roots = 0
        let g:gutentags_project_root = [ '.git', ]
    " }}}

    " Neoformat {{{
        nnoremap <leader>f :<C-u>Neoformat<CR>
    " }}}

    " Investigate {{{
        " Use Dash on macOS (if available)
        let g:investigate_use_dash=1

        nnoremap K :call investigate#Investigate('n')<CR>
        vnoremap K :call investigate#Investigate('v')<CR>
    " }}}

    " Targets {{{
        let g:targets_pairs = '()b {}B []S <>t'
    " }}}

    " vim-polyglot {{{
        let g:polyglot_disabled = ['tex', 'latex']
    " }}}

    " vim-surround {{{
        " Add a custom 'S' mapping to surround with square brackets.
        " Works together with vim-targets.
        let g:surround_{char2nr("S")} = "[\r]"
    " }}}

    " vim-fzf {{{
        let g:fzf_layout = { 'down': '~40%' }

        function! s:FZFFiles()
            try
                " If possible, launch FZF from the current project root.
                " We're using gutentags#get_project_root for the task.
                execute 'Files' gutentags#get_project_root(expand('%:p:h', 1))
            catch
                execute 'Files' expand('%:p:h', 1)
            endtry
        endfunction

        nnoremap <silent> <leader><space> :call <SID>FZFFiles()<CR>
        nnoremap <silent> <leader>a :Buffers<CR>
        nnoremap <silent> <leader>A :Ag<CR>
        nnoremap <silent> <leader>tt :BTags<CR>
        nnoremap <silent> <leader>tT :Tags<CR>
        nnoremap <silent> <leader>? :History<CR>
        nnoremap <silent> <leader>: :History:<CR>
    " }}}
" }}}

" Ending settings {{{
    if filereadable(expand('~/.vimrc.local'))
        source ~/.vimrc.local
    endif

    set secure
" }}}
